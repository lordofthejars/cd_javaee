apply plugin: 'war'
apply plugin: 'pmd'
apply plugin: 'jacoco'

group = 'org.superbiz'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7


repositories {
    mavenCentral()
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:0.5.3.RELEASE"
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.2.0'
    }
}

apply plugin: "io.spring.dependency-management"
apply plugin: 'org.unbroken-dome.test-sets'


dependencyManagement {
    imports {
        mavenBom 'org.arquillian:arquillian-universe:1.0.0.Alpha1'
    }
}

dependencies {
    testCompile group: 'org.arquillian.universe', name: 'arquillian-junit', ext: 'pom'
    testCompile group: 'org.apache.openejb', name: 'arquillian-tomee-embedded', version:'1.7.2'
    testCompile group: 'org.apache.openejb', name: 'tomee-jaxrs', version:'1.7.2'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    providedCompile group: 'org.apache.openejb', name: 'javaee-api', version:'6.0-6'
}

// ** Integration/Slow tests

testSets {
    integrationTests
}


// ** PMD Configuration **

pmd {
    ignoreFailures = true
    ruleSets = [
            "basic"
    ]
}

// ** JaCoCo configuration **

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
    }
}

check.dependsOn integrationTests
integrationTests.mustRunAfter test

// Generating reports for integration tests
task jacocoRootTestReport(type: JacocoReport) {
    sourceSets sourceSets.main
    executionData files([
            "$buildDir/jacoco/test.exec",
            "$buildDir/jacoco/integrationTests.exec"
    ])
    reports {
        xml.enabled false
        csv.enabled false
    }    
}
